#! /bin/csh -f
# Generate the mf-file-making commands for each family.

# Make sure we haven't already been run.
set output_file = make_mf_files
if (-e $output_file) then
   echo "Sorry, $output_file already exists."
   exit(1)
else
   echo '#! /bin/csh -f' > $output_file
endif

# You may want to edit this list for particular runs. Some
# of these families aren't too useful. For example:
# set families = (bx mi r sl ss sy ti tt)
set families = (b bsy bx bxsl bxti csc dunh ex ff fi fib \
inch itt mi mib r sl sltt ss ssbx ssdc ssi ssq ssqi sy tcsc \
tex ti tt u vtt)

# Now ask for the sizes.
foreach fam ($families)
   echo -n "Point sizes for cm$fam? "
   set sizes = $<

   # And write out the commands. If the name of
   # mf-file-making command changes, change it here.
   if ($sizes != '') then
      echo "make_mf cm$fam $sizes" >> $output_file
   endif
end

# Make it executable.
chmod ogu+x $output_file
echo "$output_file created."
