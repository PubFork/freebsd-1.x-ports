dnl Process this file with Autoconf to produce a configure script for TeX.
dnl
dnl We don't use (for example) tex/tex.web because people who only want
dnl to build part of the distribution may not have any given program.
dnl Even web2c.c isn't guaranteed, but then nothing is ...
AC_INIT(web2c/web2c.c)

AC_CONFIG_HEADER(lib/c-auto.h)
dnl Don't complain when common.ac uses AC_CONFIG_HEADER.
define(AC_CONFIG_HEADER,)

sinclude(../kpathsea/common.ac)

dnl Do something erroneous if the symbol we are testing is undefined.
define(user_sym_check, [AC_TEST_CPP(#include "$srcdir/lib/c-auto.h.in"
#ifndef $1
#include <nonexistxyz.kkk>
#endif
, $1=t
)])

AC_PROG_YACC
AC_PROG_LEX
AC_LN_S

AC_RETSIGTYPE

dnl These tests prevent reliable cross-compilation.  Sigh.
AC_CHAR_UNSIGNED
AC_LONG_64_BITS
AC_WORDS_BIGENDIAN

dnl Must do this last, as the addition to DEFS in Autoconf 1.3 confuses
dnl the compiler.
AC_DECLARE_YYTEXT

echo checking for window system libraries
user_sym_check(SUNWIN)
user_sym_check(XVIEWWIN)
user_sym_check(X11WIN)
user_sym_check(X10WIN)
if test -n "$XVIEWWIN"; then
  wlibs="-lxview -lolgx -lX11 $wlibs"
  (cd $srcdir/mf/MFwindow; rm -f sun.c; $LN_S sun-xview.c sun.c)
fi
if test -n "$X11WIN"; then
  AC_FIND_X
  AC_FIND_XTRA
  if test -z "$Xextlib"; then
    AC_HAVE_LIBRARY(Xext, Xextlib=-lXext)
  fi
  wlibs="-lXt $Xextlib -lX11 $wlibs"
  (cd $srcdir/mf/MFwindow; rm -f x11.c; $LN_S x11-Xt.c x11.c)
fi
if test -n "$X10WIN"; then
  wlibs="-lX $wlibs"
fi
if test -n "$SUNWIN"; then
  wlibs="-lsuntool -lsunwindow -lpixrect $wlibs"
  (cd $srcdir/mf/MFwindow; rm -f sun.c; $LN_S sun-sunview.c sun.c)
fi
AC_SUBST(wlibs)

# Take bdpi from the `default_bdpi' variable in ./Makefile.in.
changequote(,)dnl
bdpi=`sed -n '/^default_bdpi *= *\([0-9]*\).*$/s//\1/p' Makefile.in`
default_texsizes=$(printf "define m(x,y) {\nreturn (x * (1.2 ^ y))\n}\nprint \
    ${bdpi},\":\",\
    m(${bdpi},1),\":\",\
    m(${bdpi},2),\":\",\
    m(${bdpi},3),\":\",\
    m(${bdpi},4),\":\",\
    m(${bdpi},5),\"\n\"\n" | 
    bc)
changequote([,])dnl
AC_SUBST(default_texsizes)

echo checking for small TeX/MF/BibTeX
# Do TeX first.
# If `small' version is not wanted, do nothing -- make will copy the
# default change file.
user_sym_check(SMALLTeX)
if test -n "$SMALLTeX"; then
  echo "(small TeX, creating tex/ctex.ch)"
  (cd $srcdir/tex && rm -f ctex.ch && sed -f small.sed <tex.ch >ctex.ch)
  AC_DEFINE(GLUERATIO_TYPE, float)
fi

# Metafont next.
user_sym_check(SMALLMF)
if test -n "$SMALLMF"; then
  echo "(small Metafont, creating mf/cmf.ch)"
  (cd $srcdir/mf && rm -f cmf.ch && sed -f small.sed <mf.ch >cmf.ch)
fi

# And finally BibTeX.
user_sym_check(SMALLBibTeX)
if test -n "$SMALLBibTeX"; then
  echo "(small BibTeX, creating bibtex/cbibtex.ch)"
  (cd $srcdir/bibtex \
   && rm -f cbibtex.ch && sed -f small.sed <bibtex.ch >cbibtex.ch)
fi

dnl Generate `Makefile's, `config.status', and our header file.
AC_OUTPUT(Makefile bibtex/Makefile dviutil/Makefile fontutil/Makefile \
lib/Makefile man/Makefile mf/Makefile mf/MFwindow/Makefile \
tex/Makefile web/Makefile web2c/Makefile)
