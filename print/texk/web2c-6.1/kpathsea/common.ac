dnl Common Autoconf sinclude file for kpathsea-using programs.  (Have to
dnl use the m4 `sinclude' builtin instead of `include', since Autoconf
dnl disables `include'.)

dnl Write output here, instead of putting a zillion -D's on the command line.
AC_CONFIG_HEADER(c-auto.h)

AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_RANLIB

AC_ISC_POSIX

AC_DIR_HEADER
AC_XENIX_DIR
AC_STDC_HEADERS
AC_HAVE_HEADERS(assert.h float.h limits.h memory.h pwd.h stdlib.h \
                string.h unistd.h)

AC_HAVE_FUNCS(memmove putenv)

# It would be nicer to test for the requisite putenv features directly,
# but can't figure out how to do that.
if test "`(uname) 2>/dev/null`" = NetBSD \
   || test "`(uname) 2>/dev/null`" = FreeBSD; then
  AC_DEFINE(SMART_PUTENV)
fi

AC_CONST

# Any assignment to VPATH causes various Sun make's (both SunOS and
# Solaris) to only execute the first set of double-colon rules, as in
# the following 5-line Makefile. Since we use :: rules for distclean and
# other important targets, we just remove the VPATH lines if they
# defined it to be simply `.'.
# VPATH = foo
# target::
#	echo first
# target::
#	echo second

# The line we're matching here is
# VPATH = <whatever>
# with arbitrary spaces and tabs around each of the elements.
if test x$srcdir = x.; then
  changequote(,)
  extrasub='/^[ 	]*VPATH[ 	]*=[ 	]*/d'
  changequote([,])
fi
